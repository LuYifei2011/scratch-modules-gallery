!import exponentiation

定义 弹性 缓入 从 (起始值) 到 (结束值) 进度 (进度) %
将 [补间 v] 设为 ((进度) / (100))
如果 <(补间) = (0)> 那么
	将 [补间 v] 设为 (0)
否则
	如果 <(补间) = (1)> 那么
		将 [补间 v] 设为 (1)
	否则
		(2) ^ (((10) * (补间)) - (10))::custom
		将 [补间 v] 设为 (() - ((a^b) * ([sin v] ((((补间) * (10)) - (10.75)) * (120)))))
	结束
结束
将 [补间 v] 设为 (((补间) * ((结束值) - (起始值))) + (起始值))

定义 弹性 缓出 从 (起始值) 到 (结束值) 进度 (进度) %
将 [补间 v] 设为 ((进度) / (100))
如果 <(补间) = (0)> 那么
	将 [补间 v] 设为 (0)
否则
	如果 <(补间) = (1)> 那么
		将 [补间 v] 设为 (1)
	否则
		(2) ^ ((-10) * (补间))::custom
		将 [补间 v] 设为 (((a^b) * ([sin v] ((((补间) * (10)) - (0.75)) * (120)))) + (1))
	结束
结束
将 [补间 v] 设为 (((补间) * ((结束值) - (起始值))) + (起始值))

定义 弹性 缓入和出 从 (起始值) 到 (结束值) 进度 (进度) %
将 [补间 v] 设为 ((进度) / (100))
如果 <(补间) = (0)> 那么
	将 [补间 v] 设为 (0)
否则
	如果 <(补间) = (1)> 那么
		将 [补间 v] 设为 (1)
	否则
		如果 <(补间) < (0.5)> 那么
			(2) ^ (((20) * (补间)) - (10))::custom
			将 [补间 v] 设为 ((() - ((a^b) * ([sin v] ((((补间) * (20)) - (11.125)) * (80))))) / (2))
		否则
			(2) ^ (((-20) * (补间)) + (10))::custom
			将 [补间 v] 设为 ((((a^b) * ([sin v] ((((补间) * (20)) - (11.125)) * (80)))) / (2)) + (1))
		结束
	结束
结束
将 [补间 v] 设为 (((补间) * ((结束值) - (起始值))) + (起始值))