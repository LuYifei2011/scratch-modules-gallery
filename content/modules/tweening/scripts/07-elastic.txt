!import exponentiation

define elastic ease in from (start :: custom-arg) to (end :: custom-arg) progress (progress% :: custom-arg)
set [Tween v] to ((progress% :: custom-arg) / (100))
if <(Tween) = (0)> then
  set [Tween v] to (0)
 else
  if <(Tween) = (1)> then
    set [Tween v] to (1)
   else
    (2) ^ (((10) * (Tween)) - (10)) :: custom
    set [Tween v] to (() - ((a^b) * ([sin v] of ((((Tween) * (10)) - (10.75)) * (120)))))
  end
end
set [Tween v] to (((Tween) * ((end :: custom-arg) - (start :: custom-arg))) + (start :: custom-arg))

define elastic ease out from (start :: custom-arg) to (end :: custom-arg) progress (progress% :: custom-arg)
set [Tween v] to ((progress% :: custom-arg) / (100))
if <(Tween) = (0)> then
  set [Tween v] to (0)
 else
  if <(Tween) = (1)> then
    set [Tween v] to (1)
   else
    (2) ^ ((-10) * (Tween)) :: custom
    set [Tween v] to (((a^b) * ([sin v] of ((((Tween) * (10)) - (0.75)) * (120)))) + (1))
  end
end
set [Tween v] to (((Tween) * ((end :: custom-arg) - (start :: custom-arg))) + (start :: custom-arg))

define elastic ease in-out from (start :: custom-arg) to (end :: custom-arg) progress (progress% :: custom-arg)
set [Tween v] to ((progress% :: custom-arg) / (100))
if <(Tween) = (0)> then
  set [Tween v] to (0)
 else
  if <(Tween) = (1)> then
    set [Tween v] to (1)
   else
    if <(Tween) < (0.5)> then
      (2) ^ (((20) * (Tween)) - (10)) :: custom
      set [Tween v] to ((() - ((a^b) * ([sin v] of ((((Tween) * (20)) - (11.125)) * (80))))) / (2))
     else
      (2) ^ (((-20) * (Tween)) + (10)) :: custom
      set [Tween v] to ((((a^b) * ([sin v] of ((((Tween) * (20)) - (11.125)) * (80)))) / (2)) + (1))
    end
  end
end
set [Tween v] to (((Tween) * ((end :: custom-arg) - (start :: custom-arg))) + (start :: custom-arg))